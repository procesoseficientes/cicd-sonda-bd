-- =============================================
-- Author:         diego.as
-- Create date:    15-02-2016
-- Description:    Actualiza registros en la Tabla 
--				   [acsa].[OP_WMS_CERTIFICATE_DEPOSIT_DETAIL]
--				   en base a los parámetros:
/*
						[CERTIFICATE_DEPOSIT_ID_DETAIL]
						[CERTIFICATE_DEPOSIT_ID_HEADER]
*/  
--				   con transacción y control de errores.
/*
Ejemplo de Ejecucion:
				--
				EXEC acsa.OP_WMS_SP_UPDATE_CERTIFICATE_DEPOSIT_DETAIL 
					@ID_DEPOSIT_DETAIL = 1
					,@ID_DEPOSIT_HEADER = 1
					,@DOC_ID = 49231
					,@MATERIAL_CODE = '96000988'
					,@SKU_DESCRIPTION = 'NARROW CHISEL 600MM'
					,@LOCATIONS = 'RACK G'
					,@BULTOS = 105
					,@QTY = 540.06
					,@CUSTOM_AMOUNT = 56706.30
					,@MESSAGE = NULL

				SELECT * FROM [acsa].[OP_WMS_CERTIFICATE_DEPOSIT_DETAIL]
				--	
*/
-- =============================================

CREATE PROCEDURE [acsa].[OP_WMS_SP_UPDATE_CERTIFICATE_DEPOSIT_DETAIL]
(
	@ID_DEPOSIT_DETAIL INT
	,@ID_DEPOSIT_HEADER INT
	,@DOC_ID NUMERIC
	,@MATERIAL_CODE VARCHAR(50)
	,@SKU_DESCRIPTION VARCHAR(200)
	,@LOCATIONS VARCHAR(200)
	,@BULTOS NUMERIC(18,0)
	,@QTY NUMERIC(18,3)
	,@CUSTOM_AMOUNT NUMERIC(18,2)
	,@MESSAGE VARCHAR(MAX) OUTPUT
)
AS
BEGIN
    SET NOCOUNT ON;
	--
	DECLARE @STATUS VARCHAR(25)

    BEGIN TRAN TransUpdate
	BEGIN TRY
	-----------------------------------------
	SELECT @STATUS = CH.STATUS FROM [acsa].[OP_WMS_CERTIFICATE_DEPOSIT_HEADER] AS CH WHERE CH.CERTIFICATE_DEPOSIT_ID_HEADER = @ID_DEPOSIT_HEADER
	-----------------------------------------

	IF (@STATUS = 'CANCELADO') BEGIN

		SET @MESSAGE = 'NO ESTÁ PERMITIDO ACTUALIZAR UN CERTIFICADO QUE YA SE ENCUENTRA CANCELADO.'
	END
	ELSE BEGIN
	
	--- Actualiza el Detalle del Certificado------------
	UPDATE [acsa].[OP_WMS_CERTIFICATE_DEPOSIT_DETAIL]
	SET [DOC_ID] = @DOC_ID
		,[MATERIAL_CODE] = @MATERIAL_CODE
		,[SKU_DESCRIPTION] = @SKU_DESCRIPTION
		,[LOCATIONS] = @LOCATIONS
		,[BULTOS] = @BULTOS
		,[QTY] = @QTY
		,[CUSTOMS_AMOUNT] = @CUSTOM_AMOUNT
	WHERE [CERTIFICATE_DEPOSIT_ID_DETAIL] = @ID_DEPOSIT_DETAIL
			AND [CERTIFICATE_DEPOSIT_ID_HEADER] = @ID_DEPOSIT_HEADER
			AND [DOC_ID] = @DOC_ID
			
	--- Actualiza el campo LAST_UPDATE del Encabezado --------------
	UPDATE [acsa].[OP_WMS_CERTIFICATE_DEPOSIT_HEADER] 
	SET LAST_UPDATED = GETDATE()
	WHERE [CERTIFICATE_DEPOSIT_ID_HEADER] = @ID_DEPOSIT_HEADER 
	
	--
	SET @MESSAGE = 'ACTUALIZACIÓN REALIZADA CON ÉXITO.'
	--
	END

    COMMIT TRAN TransUpdate

	SELECT @MESSAGE AS RESULTADO

    END TRY
    BEGIN CATCH
	    ROLLBACK
		DECLARE @ERROR VARCHAR(1000)= ERROR_MESSAGE()
		RAISERROR (@ERROR,16,1)
	END CATCH
END
